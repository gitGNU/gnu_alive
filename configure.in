dnl Process this file with autoconf to produce a configure script.

AC_INIT(qadsl, 1.3.1-pre1,
        [https://savannah.gnu.org/bugs/?group=qadsl])

dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AM_CONFIG_HEADER(include/settings.h)
AC_CONFIG_SRCDIR(configure.in)

dnl Every other copy of the package version number gets its value from here
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AC_SUBST(VERSION)

ISODATE=`date +%Y-%m-%d`
AC_SUBST(ISODATE)

AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_CC
AM_PROG_LEX
AC_PROG_YACC

dnl Checks for libraries.
AC_PROG_RANLIB

CFLAGS="-std=gnu99"

AC_ARG_ENABLE(debug, 
              AC_HELP_STRING([--enable-debug], [Enable debugging with GDB [default=no]]),[
CFLAGS="$CFLAGS -g -DDEBUG -DYYERROR_VERBOSE"],[
CFLAGS="$CFLAGS -O2"
LDFLAGS="-s"])

AC_ARG_ENABLE(pedantic-compiler,
              AC_HELP_STRING([--enable-pedantic-compiler], [Enable pedantic checks in GCC [default=no]]), 
              [CFLAGS="$CFLAGS -Wall -pedantic -Wformat=2 -Wunreachable-code -Winline"])


dnl Checks for standard header files:
dnl ANSI C header files, sys/types.h, sys/stat.h, stdlib.h, string.h,
dnl memory.h, strings.h, inttypes.h, stdint.h, unistd.h
AC_HEADER_STDC
dnl Check for qadsl specific includes
AC_CHECK_HEADERS(errno.h netdb.h stdio.h)

dnl jm_CHECK_TYPE_STRUCT_UTIMBUF
dnl AC_HEADER_MAJOR
dnl AC_FUNC_ALLOCA
AC_STRUCT_TM
dnl AC_STRUCT_ST_BLOCKS
dnl AC_FUNC_CLOSEDIR_VOID

AH_TOP([/* settings.h - Created by configure */
#ifndef __SETTINGS_H__
#define __SETTINGS_H__])
AH_BOTTOM([#endif /* __SETTINGS_H__ */])

AH_VERBATIM([_GNU_SOURCE], [
/* Enable GNU extensions on systems that have them.  */
#ifndef _GNU_SOURCE
# define _GNU_SOURCE
#endif])

dnl Look for implementations for these:
AC_CHECK_FUNCS()

dnl 
dnl XXX - Much of these fine checks are taken from QuakeForge.net
dnl 

dnl Checks for stricmp/strcasecmp
AC_CHECK_FUNC(strcasecmp, strcasecmp=yes, strcasecmp=no)
if test $strcasecmp = no; then
	AC_CHECK_FUNC(stricmp,
		AC_DEFINE(strcasecmp, stricmp, [Define strcasecmp as stricmp if you have one but not the other]),
		AC_MSG_ERROR([Neither stricmp nor strcasecmp found])
	)
fi

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.

dnl Check for various --with-??? directives

default_globalconf="/etc/${PACKAGE}.conf"
AC_ARG_WITH(global-cfg,
[
qADSL Features:
  --with-global-cfg=FILE  If set will change the name and location of the
                          global configuration file used by qADSL. 
                          Defaults to /etc/qadsl.conf],
globalconf="$withval", globalconf="auto")
if test "x$globalconf" = "xauto" || test "x$globalconf" = "xyes" || \
	test "x$globalconf" = "xno"; then  dnl yes/no sanity checks
	globalconf="$default_globalconf"
fi
AC_DEFINE_UNQUOTED(GLOBAL_CONF, "$globalconf", [Define this to the location of the global config file])

default_userconf="~/.${PACKAGE}rc"
AC_ARG_WITH(user-cfg,
[  --with-user-cfg=FILE    If set will change the name and location of the
                          user-specific configuration file used by qADSL.
                          Defaults to ~/.qadslrc],
userconf="$withval", userconf="auto")
if test "x$userconf" = "xauto" || test "x$userconf" = "xyes" || \
	test "x$userconf" = "xno"; then  dnl yes/no sanity checks
	userconf="$default_userconf"
fi
AC_DEFINE_UNQUOTED(USER_CONF, "$userconf", [Define this to the location of the user config file])

default_pidfile="/var/run/qadsl.pid"
AC_ARG_WITH(pidfile,
[  --with-pidfile=FILE     If set will change the name and location of the file
                          used by qADSL to store the PID of its daemon process.
			  Defaults to /var/run/qadsl.pid],
pidfile="$withval", pidfile="auto")
if test "x$pidfile" = "xauto" || test "x$pidfile" = "xyes" || \
	test "x$pidfile" = "xno"; then  dnl yes/no sanity checks
	pidfile="$default_pidfile"
fi
AC_DEFINE_UNQUOTED(PID_FILE, "$pidfile", [Define this to the location of the user config file])

default_port=80
AC_ARG_WITH(port,
[  --with-port=NUMBER      Set the HTTP port number, defaults to standard port 80.],
port="$withval", port="auto")
if test "x$port" = "xauto" || test "x$port" = "xyes" || \
	test "x$port" = "xno"; then  dnl yes/no sanity checks
	port="$default_port"
fi
AC_DEFINE_UNQUOTED(PORT, "$port", [Define this to the HTTP port number.])


dnl XXX - These should be removed ASAP! /Joachim
AC_DEFINE(MAXDATASIZE, 4091, [I am a hack, please send patch to remove me.])
AC_DEFINE(MAXLEN, 256, [I am a hack, please send patch to remove me.])


dnl Create all the makefiles and stuff
AC_OUTPUT([
	Makefile
	qadsl.lsm
	qadsl.spec
	lib/Makefile
	src/Makefile
	etc/Makefile
	man/Makefile
])
